import "utils/pack/bool/nonStrictUnpack256" as unpack256;
from "ecc/edwards" import scalarMul;

/// Verifies match of a given public/private keypair.
///
///    Checks if the following equation holds for the provided keypair:
///    pk = sk*G
///    where G is the chosen base point of the subgroup
///    and * denotes scalar multiplication in the subgroup
///
/// Arguments:
///    pk: Curve point (public key)
///    sk: Private key
///    G: Generator point
///    EDWARDS_A: Coefficient `a` of the twisted Edwards curve
///    EDWARDS_D: Coefficient `d` of the twisted Edwards curve
///
/// Returns true for pk/sk being a valid keypair, false otherwise.
def main(field[2] pk, field sk, field[2] G, field EDWARDS_A, field EDWARDS_D) -> bool {
    bool[256] sk_bits = unpack256(sk);
    field[2] res = scalarMul(sk_bits, G, EDWARDS_A, EDWARDS_D);
    return (res[0] == pk[0] && res[1] == pk[1]);
}